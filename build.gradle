plugins {
  id "com.github.hierynomus.license" version "0.14.0"
  id 'nebula.release' version '6.0.0'
}

apply plugin: 'java'

version = "1.3.5"

if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

if (!project.hasProperty('release.useLastTag')) {
  project.ext['release.useLastTag'] = true
}


task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'CI Migration Tool',
        'Implementation-Version': version,
        'Main-Class': 'com.xebialabs.migration.Migration'
    }
    baseName = 'xl_ci_tool_exec'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test {
    // Auto detected Unit tests only, exclude the end to end integration test
    exclude '**/*IntegrationTest.class'
    
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    compile 'com.google.guava:guava:23.0'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

    // https://mvnrepository.com/artifact/com.typesafe/config
    compile group: 'com.typesafe', name: 'config', version: '1.3.3'


    // Use JUnit test framework
    testCompile 'junit:junit:4.12'

}


// In this section you declare where to find the dependencies of your project
repositories {
    mavenLocal()
    mavenCentral()
    maven {
    url 'https://dist.xebialabs.com/public/maven2'
  }
  maven {
    url 'http://www.knopflerfish.org/maven2'
  }
}

license {
  header rootProject.file('License.md')
  strictCheck false 
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.name = 'XEBIALABS'
}


